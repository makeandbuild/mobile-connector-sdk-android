def globalVersion = new Version("1.1.0")

// Define the artifacts coordinates
group = 'com.silverpop'
version = globalVersion
status = version.status

apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    //Place repository you would like to push EngageSDK to here
//    maven {
//        url 'http://localhost:8081/artifactory/libs-release-local'
//    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    sourceSets {
        androidTest {
            java.srcDirs = ['src/androidTest/java']
        }
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    //Uncomment the following lines if you wish to deploy EngageSDK to custom repository
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        myConfig {
            storeFile file("other.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebugBuild true
            //signingConfig signingConfigs.myConfig
        }
        release {
            runProguard false
        }
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

//Singing the library for release version.
signing {
    /*required {
        version.status == 'release' && gradle.taskGraph.hasTask("uploadArchives")
    }*/
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'EngageSDK'
            packaging 'aar'
            description 'Silverpop Android EngageSDK'
            url 'https://github.com/Silverpop/mobile-connector-sdk-android'

            scm {
              url 'scm:git@github.com:Silverpop/mobile-connector-sdk-android.git'
              connection 'scm:git@github.com:Silverpop/mobile-connector-sdk-android.git'
              developerConnection 'scm:git@github.com:Silverpop/mobile-connector-sdk-android.git'
            }

            licenses {
              license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
              }
            }

            developers {
                developer {
                    id 'slytle'
                    name 'Silverpop Development'
                    email 'slytle@us.ibm.com'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:[19,20)'

    //Setup local test compile
    androidTestCompile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile 'com.android.support:appcompat-v7:[19,20)'
    androidTestCompile 'org.assertj:assertj-core:1.7.1'
}

class Version {
    String buildVersion
    String status

    Version(String versionValue) {
        buildVersion = versionValue
        if (buildVersion.endsWith('-SNAPSHOT')) {
            status = 'integration'
        } else {
            status = 'release'
        }
    }

    String toString() {
        buildVersion
    }
}
